import groovy.json.JsonSlurper

apply from: 'gradle/grgit.gradle'

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28

    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        applicationId "gr.thmmy.mthmmy"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 18
        versionName "1.6.3"
        archivesBaseName = "mTHMMY-v$versionName"
        buildConfigField "String", "CURRENT_BRANCH", "\"" + getCurrentBranch() + "\""
        buildConfigField "String", "COMMIT_HASH", "\"" + getCommitHash() + "\""
        buildConfigField "boolean", "IS_CLEAN", String.valueOf(isClean())
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            def date = new Date().format('ddMMyy_HHmmss')
            archivesBaseName = archivesBaseName + "-$date"
            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.contains("assembleRelease")) {
        task.getDependsOn().add({
            def inputFile = new File("app/google-services.json")
            def json = new JsonSlurper().parseText(inputFile.text)
            if (json.project_info.project_id != "mthmmy-release-3aef0")
                throw new GradleException('Please supply the correct google-services.json for release (or manually change the id above)!')
        })
    } else if (task.name.contains("assembleDebug")) {
        task.getDependsOn().add({
            def inputFile = new File("app/google-services.json")
            def json = new JsonSlurper().parseText(inputFile.text)
            if (json.project_info.project_id == "mthmmy-release-3aef0")
                throw new GradleException('Please replace the release google-services.json with a debug one!')
        })
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.preference:preference:1.1.0-alpha02'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.google.firebase:firebase-core:16.0.6'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.8'
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'
    implementation 'org.jsoup:jsoup:1.10.3' //TODO: Warning: upgrading from 1.10.3 will break stuff!
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'com.mikepenz:materialdrawer:6.1.1'
    implementation 'com.mikepenz:fontawesome-typeface:4.7.0.0@aar'
    implementation 'com.mikepenz:google-material-typeface:3.0.1.2.original@aar'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.15'
    implementation 'com.bignerdranch.android:expandablerecyclerview:3.0.0-RC1'//TODO: deprecated!
    implementation 'me.zhanghai.android.materialprogressbar:library:1.4.2'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'ru.noties:markwon:2.0.0'
    implementation 'net.gotev:uploadservice:3.4.2'
    implementation 'net.gotev:uploadservice-okhttp:3.4.2'
    implementation 'com.itkacher.okhttpprofiler:okhttpprofiler:1.0.4' //Plugin: https://plugins.jetbrains.com/plugin/11249-okhttp-profiler
}

apply plugin: 'com.google.gms.google-services'
